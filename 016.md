Delightful Merlot Iguana

high

# PartyB with Positive UPNL Can Prevent Users from Closing Their Positions
## Summary

PartyB, when possessing a positive UPNL, can deallocate all its funds. This prevents a user from closing a profitable
position due to an underflow error occurring in the `LibQuote.closeQuote` method.

## Vulnerability Detail

Let's consider the following scenario: A user holds two positions with a specific PartyB. One of these positions incurs
a significant loss, while the other yields a modest profit. PartyB, having a sizable positive
UPNL, can deallocate all the funds allocated to that user.

Here's the relevant code snippet for deallocating funds:

```ts
 function deallocateForPartyB(
  uint256 amount,
  address partyA,
  SingleUpnlSig memory upnlSig
) internal {
  AccountStorage.Layout storage accountLayout = AccountStorage.layout();
  require(
    accountLayout.partyBAllocatedBalances[msg.sender][partyA] >= amount,
    "PartyBFacet: Insufficient locked balance"
  );
  LibMuon.verifyPartyBUpnl(upnlSig, msg.sender, partyA);
  int256 availableBalance = LibAccount.partyBAvailableForQuote(
    upnlSig.upnl,
    msg.sender,
    partyA
  );
  require(availableBalance >= 0, "PartyBFacet: Available balance is lower than zero");
  require(uint256(availableBalance) >= amount, "PartyBFacet: Will be liquidatable");

  accountLayout.partyBNonces[msg.sender][partyA] += 1;
  accountLayout.partyBAllocatedBalances[msg.sender][partyA] -= amount;
  accountLayout.balances[msg.sender] += amount;
  accountLayout.withdrawCooldown[msg.sender] = block.timestamp;
}
```

Following the deallocation, the user is unable to close the profitable position. The error occurs in this line within
the `closeQuote` method:

```ts
function closeQuote(Quote storage quote, uint256 filledAmount, uint256 closedPrice) internal {
  //...
  if (hasMadeProfit) {
    accountLayout.allocatedBalances[quote.partyA] += pnl;
    accountLayout.partyBAllocatedBalances[quote.partyB][quote.partyA] -= pnl;  // This line will throw an error!
  } else {
    accountLayout.allocatedBalances[quote.partyA] -= pnl;
    accountLayout.partyBAllocatedBalances[quote.partyB][quote.partyA] += pnl;
  }
  //..
}
```


## Impact

Users may find themselves unable to close profitable positions if they are engaged with a malicious PartyB.

## Code Snippet

Provided in detail section

## Tool used

Manual Review

## Recommendation

Not provided