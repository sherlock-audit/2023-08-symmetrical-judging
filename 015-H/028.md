Blunt Butter Moose

high

# `depositAndAllocateForAccount` is broken due to incorrect precision
## Summary

Due to incorrect precision, any users or external protocols utilizing the `depositAndAllocateForAccount` to allocate 1000 USDC will end up only having 0.000000001 USDC allocated to their account. This might potentially lead to unexpected loss of funds due to the broken functionality if they rely on the accuracy of the function outcome to perform certain actions that deal with funds/assets.

## Vulnerability Detail

If the collateral is USDC, then `amount` will be in 6 decimals. This amount is converted into 18 decimals internally after the `depositFor` function is executed at Line 168.

The `allocate` function executed at Line 173 expects the amount to be internal precision (18 decimals). However, the amount passed into this amount is in 6 decimals.

https://github.com/sherlock-audit/2023-08-symmetrical/blob/main/symmio-core/contracts/multiAccount/MultiAccount.sol#L157

```solidity
File: MultiAccount.sol
157:     function depositAndAllocateForAccount(
158:         address account,
159:         uint256 amount
160:     ) external onlyOwner(account, msg.sender) whenNotPaused {
161:         address collateral = ISymmio(symmioAddress).getCollateral();
162:         IERC20Upgradeable(collateral).safeTransferFrom(
163:             msg.sender,
164:             address(this),
165:             amount
166:         );
167:         IERC20Upgradeable(collateral).safeApprove(symmioAddress, amount);
168:         ISymmio(symmioAddress).depositFor(account, amount);
169:         bytes memory _callData = abi.encodeWithSignature(
170:             "allocate(uint256)",
171:             amount
172:         );
173:         innerCall(account, _callData);
174:         emit AllocateForAccount(msg.sender, account, amount);
175:         emit DepositForAccount(msg.sender, account, amount);
176:     }
```

Any users or external protocols utilizing the `depositAndAllocateForPartyB` to allocate 1000 USDC will end up only having 0.000000001 USDC allocated to their account, which might potentially lead to unexpected loss of funds due to the broken functionality if they rely on the accuracy of the outcome to perform certain actions dealing with funds/assets.

## Impact

This is the same high-risk bug documented in [Issue 222](https://github.com/sherlock-audit/2023-06-symmetrical-judging/issues/222) during the main audit but in a different contract. Thus, the risk impact remains the same as High.

## Code Snippet

https://github.com/sherlock-audit/2023-08-symmetrical/blob/main/symmio-core/contracts/multiAccount/MultiAccount.sol#L157

## Tool used

Manual Review

## Recommendation

Consider making the following change:

```diff
function depositAndAllocateForAccount(
    address account,
    uint256 amount
) external onlyOwner(account, msg.sender) whenNotPaused {
    address collateral = ISymmio(symmioAddress).getCollateral();
    IERC20Upgradeable(collateral).safeTransferFrom(
        msg.sender,
        address(this),
        amount
    );
    IERC20Upgradeable(collateral).safeApprove(symmioAddress, amount);
    ISymmio(symmioAddress).depositFor(account, amount);
+   uint256 amountWith18Decimals = (amount * 1e18) /
+   (10 ** IERC20Metadata(GlobalAppStorage.layout().collateral).decimals());
    bytes memory _callData = abi.encodeWithSignature(
        "allocate(uint256)",
-       amount
+		amountWith18Decimals
    );
    innerCall(account, _callData);
    emit AllocateForAccount(msg.sender, account, amount);
    emit DepositForAccount(msg.sender, account, amount);
}
```